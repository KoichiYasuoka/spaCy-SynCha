#! /usr/bin/awk -f
# "unidic2ipadic" by 安岡孝一
BEGIN{
  u2i["名詞,普通名詞,一般,*"]="名詞,一般,*,*";
  u2i["名詞,普通名詞,サ変可能,*"]="名詞,サ変接続,*,*";
  u2i["名詞,普通名詞,サ変形状詞可能,*"]="名詞,サ変接続,*,*";
  u2i["名詞,普通名詞,副詞可能,*"]="名詞,副詞可能,*,*";
  u2i["名詞,普通名詞,形状詞可能,*"]="名詞,形容動詞語幹,*,*";
  u2i["名詞,普通名詞,助数詞可能,*"]="名詞,接尾,助数詞,*";
  u2i["名詞,助動詞語幹,*,*"]="名詞,接尾,助動詞語幹,*";
  u2i["名詞,固有名詞,一般,*"]="名詞,固有名詞,一般,*";
  u2i["名詞,固有名詞,人名,一般"]="名詞,固有名詞,人名,一般";
  u2i["名詞,固有名詞,人名,姓"]="名詞,固有名詞,人名,姓";
  u2i["名詞,固有名詞,人名,名"]="名詞,固有名詞,人名,名";
  u2i["名詞,固有名詞,地名,一般"]="名詞,固有名詞,地域,一般";
  u2i["名詞,固有名詞,地名,国"]="名詞,固有名詞,地域,国";
  u2i["名詞,数詞,*,*"]="名詞,数,*,*";
  u2i["代名詞,*,*,*"]="名詞,代名詞,一般,*";
  u2i["動詞,一般,*,*"]="動詞,自立,*,*";
  u2i["動詞,非自立可能,*,*"]="動詞,非自立,*,*";
  u2i["助動詞,*,*,*"]="助動詞,*,*,*";
  u2i["形容詞,一般,*,*"]="形容詞,自立,*,*";
  u2i["形容詞,非自立可能,*,*"]="形容詞,非自立,*,*";
  u2i["形状詞,一般,*,*"]="名詞,形容動詞語幹,*,*";
  u2i["形状詞,タリ,*,*"]="名詞,形容動詞語幹,*,*";
  u2i["形状詞,助動詞語幹,*,*"]="名詞,接尾,助動詞語幹,*";
  u2i["連体詞,*,*,*"]="連体詞,*,*,*";
  u2i["副詞,*,*,*"]="副詞,一般,*,*";
  u2i["助詞,格助詞,*,*"]="助詞,格助詞,一般,*";
  u2i["助詞,係助詞,*,*"]="助詞,係助詞,*,*";
  u2i["助詞,副助詞,*,*"]="助詞,副助詞,*,*";
  u2i["助詞,準体助詞,*,*"]="名詞,非自立,一般,*";
  u2i["助詞,終助詞,*,*"]="助詞,終助詞,*,*";
  u2i["助詞,接続助詞,*,*"]="助詞,接続助詞,*,*";
  u2i["接続詞,*,*,*"]="接続詞,*,*,*";
  u2i["接頭辞,*,*,*"]="接頭詞,名詞接続,*,*";
  u2i["接尾辞,名詞的,一般,*"]="名詞,接尾,一般,*";
  u2i["接尾辞,名詞的,サ変可能,*"]="名詞,接尾,サ変接続,*";
  u2i["接尾辞,名詞的,副詞可能,*"]="名詞,接尾,副詞可能,*";
  u2i["接尾辞,名詞的,助数詞,*"]="名詞,接尾,助数詞,*";
  u2i["接尾辞,動詞的,*,*"]="動詞,接尾,*,*";
  u2i["接尾辞,形容詞的,*,*"]="形容詞,接尾,*,*";
  u2i["接尾辞,形状詞的,*,*"]="名詞,接尾,形容動詞語幹,*";
  u2i["感動詞,一般,*,*"]="感動詞,*,*,*";
  u2i["感動詞,フィラー,*,*"]="フィラー,*,*,*";
  u2i["補助記号,句点,*,*"]="記号,句点,*,*";
  u2i["補助記号,読点,*,*"]="記号,読点,*,*";
  u2i["補助記号,括弧開,*,*"]="記号,括弧開,*,*";
  u2i["補助記号,括弧閉,*,*"]="記号,括弧閉,*,*";
  u2i["補助記号,ＡＡ,一般,*"]="記号,アルファベット,*,*";
  u2i["補助記号,ＡＡ,顔文字,*"]="記号,一般,*,*";
  u2i["補助記号,一般,*,*"]="記号,一般,*,*";
  u2i["記号,文字,*,*"]="記号,一般,*,*";
  u2i["記号,一般,*,*"]="記号,一般,*,*";
  u2i["空白,*,*,*"]="記号,空白,*,*";
}
{
  if(NF==2||NF==3){
    split($2,a,",");
    p=a[1]","a[2]","a[3]","a[4];
    q=u2i[p];
    if(q=="")
      q=p;
    i=index(a[8],"-");
    if(i>1)
      lemma=substr(a[8],1,i-1);
    else
      lemma=a[8];
    if(lemma==""||lemma=="*")
      lemma=$1;
    i=index(a[6],"-");
    if(i>1)
      katsuyo=substr(a[6],1,i-1);
    else
      katsuyo=a[6];
    if(katsuyo=="終始形")
      katsuyo="基本形";
    else if(katsuyo=="連体形")
      katsuyo="体言接続";
    printf("%s\t%s,%s,%s,%s,%s,%s",$1,q,a[5],katsuyo,lemma,a[11],a[10]);
    if(NF==3)
      printf("\t%s\n",$3);
    else
      printf("\n");
  }
  else
    printf("%s\n",$0);
}
